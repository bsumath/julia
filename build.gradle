plugins {
    id 'de.gliderpilot.jnlp' version '0.2.1'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'de.gliderpilot.jnlp'

group = 'edu.bsu.julia'
version = '2.2.0'
description = 'Julia Fractal Drawing Program'

repositories { mavenCentral() }

dependencies {
    compile group: 'org.jogamp.gluegen', name: 'gluegen-rt-main', version: '2.3.1'
    compile group: 'org.jogamp.jogl', name: 'jogl-all-main', version: '2.3.1'
    compile group: 'org.apache.commons', name: 'commons-math', version: '2.2'
}

jnlp {
    href 'julia.jnlp'
    codebase 'https://bsumath.github.io/julia/jnlp'
    mainClassName = 'edu.bsu.julia.Julia'
    useVersions = true
    usePack200 = false

    signJarAddedManifestEntries = [
            'Codebase'        : project.jnlp.jnlpParams.codebase,
            'Permissions'     : 'all-permissions',
            'Application-Name': project.name
    ]

    withXml {
        information {
            title project.description
            vendor 'Ball State University'
            homepage href: "http://www.bsu.edu/web/rstankewitz/JuliaHelp2.0/Julia.html"
            description kind: "one-line", 'A program for the study of polynomial semigroups.'
            'offline-allowed'()
        }
        security {
            'all-permissions'()
        }
    }
}

allprojects {
   afterEvaluate { project ->
       def propsFile = rootProject.file('keystore.properties')

       if (propsFile.exists()) {
           def props = new Properties()
           props.load(new FileInputStream(propsFile))
           project.jnlp.signJarParams = props
       }
   }
}
